############################################################################################################################
#### File contains calculations that verify complements for maximal tori of G_2(q) in their algebraic normalizers.
#### Each case is divided into two parts. The first part contains commands for GAP. We verify generators and relations
#### for C_W(w).
#### The second part devotes to computations in MAGMA. We verify that specific preimages of generators of C_W(w) satisfy
#### the set of defining relations of C_W(w)
#####

#### Preparatory commands in GAP:
#### reflections and the Weyl group 
####

w1:=[[-1, 3], [ 0, 1]];;
w2:=[[1, 0], [ 1, -1]];;
w3:=[[2, -3], [ 1, -2]];;
w4:=[[-1, 0], [ -1, 1]];;
w5:=[[-2, 3], [ -1, 2]];;
w6:=[[1, -3], [ 0, -1]];;

w:=[w1,w2,w3,w4,w5,w6];
W:=Group(w1,w2);
w0:=w1*w6; ### central involution
id:=w[1]^2; ### identity matrix

###############################################################################

##### Preparatory commands in MAGMA (calculator: http://magma.maths.usyd.edu.au/calc/ ) to perform calculations

L:=LieAlgebra("G2", Rationals());
R:=RootDatum(L);
G:=GroupOfLieType(L);
X, Y, H:= ChevalleyBasis(L);  // print out extra special pairs of roots and signs
IsChevalleyBasis(L, R, X, Y, H);
n:=[ elt<G|i> : i in [1..6]];
h:=[TorusTerm(G, i,-1) : i in [1..6]];
w:=[Transpose(i) : i in ReflectionMatrices(R)];

id:=w[1]*w[1];
n0:=h[1]*n[1]*n[6];

// verify that n0 commutes with all n[i]
n0*n[1] eq n[1]*n0;
n0*n[2] eq n[2]*n0;
n0*n[3] eq n[3]*n0;
n0*n[4] eq n[4]*n0;
n0*n[5] eq n[5]*n0;
n0*n[6] eq n[6]*n0;

###############################################################################

#### Tori 1 and 4 (w=id or w=w0)

#### verify that C_W(w) is isomorphic to D12 

x:=id;
C:=Centralizer(W, x);
Order(C) = 12;   ### by construction C=W is generated by two involution

#### verify that w1, w2 generate C_W(w) and satisfy the set of relations (GAP)

a:=w[1];
b:=w[2];
a in C; b in C; 
Order(Group(a, b)) = Size(C);
a^2 = id; b^2 = id; (a*b)^6 = id; 

#### verify that h[2]*n[1], h[1]*n[2] generate a complement (MAGMA)

x:=h[2]^2; // = 1
a:=h[2]*n[1];
b:=h[1]*n[2];

a*x eq x*a;
b*x eq x*b;
a^2 eq 1; b^2 eq 1; (a*b)^6 eq 1;

#############################################################################
#### Tori 2 and 3 (w=w2 or w=w2*w0=w4)

w2*w0 = w4;

#### verify that C_W(w) is isomorphic to Z_2xZ_2 

x:=w[2];
C:=Centralizer(W,x);
IsElementaryAbelian(C);
Order(C) = 4;

#### verify that w2, w4 generate C_W(w) and satisfy the set of relations (GAP)

a:=w[2];
b:=w[4];     

a in C; b in C; 
Order(Group(a, b)) = Size(C);
a^2 = id; b^2 = id; a*b*a^-1*b^-1 = id;

#### verify that h1*n2 and h1*n4 generate a complement (MAGMA)

x:=h[1]*n[2];
x0:=h[1]*n[2]*n0;
a:=h[1]*n[2];
b:=h[1]*n[4];

a*x eq x*a;
b*x eq x*b;
a*x0 eq x0*a;
b*x0 eq x0*b;
          
a^2 eq 1; b^2 eq 1; a*b*a^-1*b^-1 eq 1;

#### verify h[2]*n[1] and h[2]*n[6] generate a complement (MAGMA)

x:=h[2]*n[1];
a:=h[2]*n[1];
b:=h[2]*n[6];

a*x eq x*a;
b*x eq x*b;
a^2 eq 1; b^2 eq 1; a*b*a^-1*b^-1 eq 1;             

#############################################################################
#### Tori 5 and 6 (w=w1*w3 or w=w1*w3*w0=w1*w5)

w1*w3*w0 = w1*w5;

#### verify that C_W(w) is isomorphic to Z_3xZ_2

x:=w[1]*w[3];
C:=Centralizer(W,x);
IsCyclic(C);
Order(C) = 6;

#### verify that w1*w3 and w1*w6 generate C_W(w) and satisfy the set of relations (GAP)

a:=w1*w3;
b:=w0;

a in C; b in C; 
Order(Group(a,b)) = Size(C);
Size(C) = 6;
a^3 = id; b^2 = id;
a*b*a^-1*b^-1 = id; 

#### verify n[1]*n[3] and h[1]*n[1]*n[6] generate a complement (MAGMA)

x:=n[1]*n[3];
x0:=n[1]*n[3]*n0;

a:=n[1]*n[3];
b:=n0;

a*x eq x*a;
b*x eq x*b;
a*x0 eq x0*a;
b*x0 eq x0*b;

a^3 eq 1; b^2 eq 1; a*b*a^-1*b^-1 eq 1;             

